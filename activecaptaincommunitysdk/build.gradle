plugins {
    id 'com.android.library'
    id 'maven-publish'
}

android {
    compileSdk 34
    namespace "com.garmin.marine.activecaptaincommunitysdk"

    defaultConfig {
        minSdk 23
        targetSdk 34
        versionCode 1
        versionName "2.0.3"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
            withJavadocJar()
        }
        singleVariant('debug') {
            withSourcesJar()
            withJavadocJar()
        }
    }

    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
            version "3.10.2"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    libraryVariants.all { variant ->
        variant.outputs.all { output ->
            if (outputFile != null && outputFileName.endsWith('.aar')) {
                outputFileName = "activecaptaincommunitysdk-${variant.name}-${defaultConfig.versionName}.aar"
            }
        }

        task("generate${variant.name}Javadoc", type: Javadoc) {
            source = android.sourceSets.main.java.srcDirs
            classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
            destinationDir = file("../doc/${variant.name}")
        }
    }
}

dependencies {
}

afterEvaluate {
    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/garmin/ActiveCaptainCommunitySDK-android")
                credentials {
                    username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USERNAME")
                    password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
                }
            }
        }
        publications {
            release(MavenPublication) {
                from components.release

                groupId = 'com.garmin.marine.activecaptaincommunitysdk'
                artifactId = 'activecaptaincommunitysdk-release'
                version = android.defaultConfig.versionName
            }

            debug(MavenPublication) {
                from components.debug

                groupId = 'com.garmin.marine.activecaptaincommunitysdk'
                artifactId = 'activecaptaincommunitysdk-debug'
                version = android.defaultConfig.versionName
            }
        }
    }
}